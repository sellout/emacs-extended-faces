{
  description = "Inheritance-based faces for Emacs";

  nixConfig = {
    ## https://github.com/NixOS/rfcs/blob/master/rfcs/0045-deprecate-url-syntax.md
    extra-experimental-features = ["no-url-literals"];
    extra-substituters = ["https://cache.garnix.io"];
    extra-trusted-public-keys = [
      "cache.garnix.io:CTFPyKSLcx5RMJKfLo5EEPUObbA78b0YQ2DTCJXqr9g="
    ];
    ## Isolate the build.
    sandbox = "relaxed";
    use-registries = false;
  };

  outputs = {
    flake-utils,
    flaky,
    nixpkgs,
    self,
    systems,
  }: let
    pname = "extended-faces";
    ename = "emacs-${pname}";

    supportedSystems = import systems;
  in
    {
      schemas = {
        inherit
          (flaky.schemas)
          overlays
          homeConfigurations
          packages
          devShells
          projectConfigurations
          checks
          formatter
          ;
      };

      overlays = {
        default = flaky.lib.elisp.overlays.default self.overlays.emacs;

        emacs = final: prev: efinal: eprev: {
          "${pname}" = self.packages.${final.system}.${ename};
        };
      };

      homeConfigurations =
        builtins.listToAttrs
        (builtins.map
          (flaky.lib.homeConfigurations.example self [
            ({pkgs, ...}: {
              ## FIXME: This package is currently not working with emacs30 (the
              ##        default Emacs in Nixpkgs 24.11). See
              ##        sellout/emacs-extended-faces#13.
              nixpkgs.config.permittedInsecurePackages = [
                "emacs-29.4"
                "emacs-with-packages-29.4"
              ];
              programs.emacs.package = pkgs.emacs29;

              programs.emacs = {
                enable = true;
                extraConfig = ''
                  ;;; -*- lexical-binding: t; -*-
                  (require '${pname})
                '';
                extraPackages = epkgs: [epkgs.${pname}];
              };
            })
          ])
          supportedSystems);
    }
    // flake-utils.lib.eachSystem supportedSystems (system: let
      pkgs = nixpkgs.legacyPackages.${system}.appendOverlays [
        flaky.overlays.default
        flaky.overlays.elisp-dependencies
      ];

      src = pkgs.lib.cleanSource ./.;
    in {
      packages = {
        default = self.packages.${system}.${ename};
        "${ename}" = pkgs.elisp.package pname src (epkgs: [epkgs.buttercup]);
      };

      projectConfigurations =
        flaky.lib.projectConfigurations.emacs-lisp {inherit pkgs self;};

      devShells =
        self.projectConfigurations.${system}.devShells
        // {default = flaky.lib.devShells.default system self [] "";};

      checks =
        self.projectConfigurations.${system}.checks
        // {
          elisp-doctor = pkgs.elisp.checks.doctor src;
          elisp-lint = pkgs.elisp.checks.lint src (_: []);
        };

      formatter = self.projectConfigurations.${system}.formatter;
    });

  inputs = {
    ## Flaky should generally be the source of truth for its inputs.
    flaky.url = "github:sellout/flaky";

    flake-utils.follows = "flaky/flake-utils";
    nixpkgs.follows = "flaky/nixpkgs";
    systems.follows = "flaky/systems";
  };
}
